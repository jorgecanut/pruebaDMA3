cmake_minimum_required(VERSION 3.14)

project(template-project LANGUAGES ASM C CXX)

set(EXECUTABLE ${PROJECT_NAME}.elf)
set(STLIB_DIR  ${CMAKE_CURRENT_LIST_DIR}/deps/ST-LIB)
set(LD_SCRIPT  ${CMAKE_SOURCE_DIR}/STM32H723ZGTX_FLASH.ld)
set(VENV_PYTHON ${CMAKE_SOURCE_DIR}/virtual/bin/python)

option(USE_ETHERNET  "Enable ethernet peripheral"                 OFF)
option(TARGET_NUCLEO "Targets the STM32H723 Nucleo dev board"     OFF)

message(STATUS "Template project: CMAKE_CROSSCOMPILING = ${CMAKE_CROSSCOMPILING}")
message(STATUS "Template project: USE_ETHERNET         = ${USE_ETHERNET}")
message(STATUS "Template project: TARGET_NUCLEO        = ${TARGET_NUCLEO}")

add_subdirectory(${STLIB_DIR})
message(STATUS "Using ST-LIB target: ${STLIB_LIBRARY}")

file(GLOB_RECURSE SOURCE_C    ${CMAKE_SOURCE_DIR}/Core/*.c)
file(GLOB_RECURSE SOURCE_CPP  ${CMAKE_SOURCE_DIR}/Core/*.cpp)
file(GLOB_RECURSE SOURCE_H    ${CMAKE_SOURCE_DIR}/Core/*.h)
file(GLOB_RECURSE SOURCE_HPP  ${CMAKE_SOURCE_DIR}/Core/*.hpp)

add_executable(${EXECUTABLE}
  ${SOURCE_C}
  ${SOURCE_CPP}
  ${SOURCE_H}
  ${SOURCE_HPP}

  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:${CMAKE_SOURCE_DIR}/Core/Startup/startup_stm32h723zgtx.s>
)

target_link_libraries(${EXECUTABLE} PRIVATE
  ${STLIB_LIBRARY}
)

set_target_properties(${EXECUTABLE} PROPERTIES
  CXX_STANDARD 23
  CXX_STANDARD_REQUIRED YES
  C_STANDARD 17
  C_STANDARD_REQUIRED YES
)

target_compile_definitions(${EXECUTABLE} PRIVATE
  $<$<BOOL:${USE_ETHERNET}>:STLIB_ETH>
  $<IF:$<BOOL:${TARGET_NUCLEO}>,NUCLEO,BOARD>
  $<IF:$<BOOL:${TARGET_NUCLEO}>,HSE_VALUE=8000000,HSE_VALUE=25000000>
)

target_compile_options(${EXECUTABLE} PRIVATE
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-mcpu=cortex-m7>
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-mfpu=fpv5-d16>
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-mfloat-abi=hard>
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-mthumb>
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-specs=nosys.specs>

  -ffunction-sections
  -fdata-sections
  -fno-exceptions

  -Wno-psabi

  $<$<COMPILE_LANGUAGE:C>:-w>

  $<$<COMPILE_LANGUAGE:CXX>:-Wall>
  $<$<COMPILE_LANGUAGE:CXX>:-Wpedantic>
  $<$<COMPILE_LANGUAGE:CXX>:-Werror>
  $<$<COMPILE_LANGUAGE:CXX>:-fno-use-cxa-atexit>
  $<$<COMPILE_LANGUAGE:CXX>:-fno-rtti>
)

target_include_directories(${EXECUTABLE} PRIVATE
  ${CMAKE_CURRENT_SOURCE_DIR}/Core/Inc
)

target_link_options(${EXECUTABLE} PRIVATE 
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-T${LD_SCRIPT}>
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-mcpu=cortex-m7>
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-mthumb>
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-mfpu=fpv5-d16>
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-mfloat-abi=hard>
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-specs=nosys.specs>
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-lc>
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-lm>
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-lnosys>
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-Wl,-Map=${PROJECT_NAME}.map,--cref>
  $<$<BOOL:${CMAKE_CROSSCOMPILING}>:-Wl,--gc-sections>
)

add_custom_target(generate_binary_metadata ALL
  COMMAND ${VENV_PYTHON} ${CMAKE_SOURCE_DIR}/tools/generate_binary_metadata.py
  WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
  COMMENT "Generating binary metadata"
)
add_dependencies(${EXECUTABLE} generate_binary_metadata)

if (PROJECT_IS_TOP_LEVEL)
  execute_process(
    COMMAND ${CMAKE_COMMAND} -E create_symlink
      ${CMAKE_BINARY_DIR}/compile_commands.json
      ${CMAKE_CURRENT_SOURCE_DIR}/compile_commands.json
  )

  add_custom_command(
    TARGET ${EXECUTABLE}
    POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E make_directory
            ${CMAKE_CURRENT_SOURCE_DIR}/out/build
    COMMAND ${CMAKE_COMMAND} -E create_symlink
            $<TARGET_FILE:${EXECUTABLE}>
            ${CMAKE_CURRENT_SOURCE_DIR}/out/build/latest.elf
  )
endif()